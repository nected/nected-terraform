
# Note: The service account name is generated by Helm as: <release-name>-sa-<chart-name>
resource "azurerm_federated_identity_credential" "agic" {
  name                = "${var.project}-agic-federated-credential-${var.environment}"
  resource_group_name = local.resource_group_name
  parent_id           = azurerm_user_assigned_identity.identity.id
  audience            = ["api://AzureADTokenExchange"]
  issuer              = azurerm_kubernetes_cluster.k8s.oidc_issuer_url
  subject             = "system:serviceaccount:agic-system:application-gateway-ingress-controller-sa-ingress-azure"

  depends_on = [
    azurerm_user_assigned_identity.identity,
    azurerm_kubernetes_cluster.k8s
  ]
}

resource "helm_release" "agic" {
  name       = "application-gateway-ingress-controller"
  repository = "oci://mcr.microsoft.com/azure-application-gateway/charts"
  chart      = "ingress-azure"
  namespace  = "agic-system"
  version    = "1.8.1"

  create_namespace = true
  depends_on = [
    azurerm_application_gateway.appgw,
    azurerm_kubernetes_cluster.k8s,
    azurerm_federated_identity_credential.agic,
    azurerm_role_assignment.appgw_identity_contributor,
    azurerm_role_assignment.appgw_identity_reader,
    azurerm_role_assignment.appgw_identity_aks_reader,
    azurerm_role_assignment.appgw_identity_operator,
    azurerm_role_assignment.appgw_identity_node_rg_reader
  ]

  values = [
    yamlencode({
      appgw = {
        subscriptionId = local.subscription_id
        resourceGroup  = local.resource_group_name
        name           = azurerm_application_gateway.appgw.name
        usePrivateIP   = false
      }
      armAuth = {
        type               = "workloadIdentity"
        identityResourceID = azurerm_user_assigned_identity.identity.id
        identityClientID   = azurerm_user_assigned_identity.identity.client_id
      }
      rbac = {
        enabled = true
      }
      kubernetes = {
        watchNamespace = ""
      }
    })
  ]
}